apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'


android {
    compileSdkVersion xTargetSdkVersion
    buildToolsVersion xBuildToolsVersion

    defaultConfig {
        versionCode 1
        minSdkVersion 11
        targetSdkVersion xTargetSdkVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled true
        addDefaultAdapters true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations {
    exported
    compile.extendsFrom exported
    all {
        resolutionStrategy {
            force "com.android.support:support-v4:$androidSupportVersion"
        }
    }
}

conf2ScopeMappings.addMapping 100, configurations.exported, 'compile'
conf2ScopeMappings.addMapping 200, configurations.compile, 'provided'

configurations.default {
    exclude group: 'com.android.databinding'
}

dependencies {
    exported "com.android.support:support-core-ui:$androidSupportVersion"
    provided "com.android.support:support-annotations:$androidSupportVersion"
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    classpath += configurations.provided
    options {
        links "https://d.android.com/reference/"
        linksOffline "https://d.android.com/reference/", "${android.sdkDirectory}/docs/reference/"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
